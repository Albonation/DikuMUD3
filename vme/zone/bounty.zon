/*
filename    bounty
password    release
changedby   Sender
EmailAdd	chris.spanton@gmail.com
request     compile
version     1
END HEADER*/

#include <macros.h>

#define JOIN_ONGOING "Becoming a Bounty Hunter"
#define JOIN_COMPLETE "Bounty Hunter"

/* ================================================================== */
/*                                ZONE                                */
/* ================================================================== */

%zone bounty

lifespan 10
reset RESET_ANYHOW
title "Bounty Hunters Guild@Mainland"
creators {"sender"}

notes
"This is the guild of the bounty hunters. In version 1.0 we introduce some 
early implementations of gamification features taken from modern gaming. 
There is significant opportunity in the future to expand on this. 

Expanding rewards, new types of bounties, bonuses, loot boxes (game money 
not IRL money), adding a cost to bounties, teaching
skills / spells to aid in bounty hunting, etc are all on the table.

Leaderboards are also a significant part of the process, that is not yet 
implemented as of v1.0. There is some code in the zone to implement that, but
it seemed a messy and cumbersome implementation. I'll look to leverage existing 
competition zone infrastructure for future versions.

///////////////////////////////
get 30 daily bounty streak
talk to bounty hunter, accept question
pray to enter sanctum, feast
talk to the guide. Get ability to feast.
talk to the historian. 
talk to the blacksmith. 
	do quest?
	Get ability to list arms/armors.
talk to the jeweler. 
	do quest?
	Get ability to buy list jewely.
talk to the historian. 
	Historian will clarify ask for playing spy on Al Kazeem. (maybe captain)?
Talk to Al Kazeem
	Either choose to betray the bounty hunters, by alerting targets
	
Jewelry, as well as arms/armor use birthstones and... perhaps some other material?
for crafting. Crafting these items allows creation of a wide range of items, with each
birthstone representing a unique ability or other boost.

// spree - how does it fit in the story?
// who gives it? Who rewards it?

Special thanks to Jager, for unwittingly sharing his package delivery scheme.
It's a perfect fit for newbies to enjoy bounties! Thanks brother. :)

Special thanks to Papi for creating the login streak bounties. It was a great idea!

"

help
"The Guild of the Bounty Hunters is an ancient institution that has survived 
and indeed thrived on the outskirts of Midgaard. Wealthy patrons who need 
something handled, a back-door to men of law finding their mark, and any 
number of other needs have been brought before the bounty hunters."

/* ================================================================== */
/*                               DIL                                  */
/* ================================================================== */

/* Bounty System!

There are a few helper functions here to assist with the bounty system.

integer add_bp@bounty(n : integer, u : unitptr);

n - the number of bounty points to add or subtract.
u - the unitptr to the unit which you want to interact.

Function is useful to safely adding or subtracting bounty points.
Will return the number of bounty points AFTER the transaction has processed.
If the value is less than 0, bounty points will be set to 0, 
and that is waht will return.

To use this as a check in the case of a situation where you dont want to allow
it to go below 0, such as in a store to use the bp in a transaction, simply 
pass 0 as the n value, and it will return the current number of bounty points.



*/


%dil

// Called when a player logs in.
//
//
dilbegin login_event();
external
   login_streak();
code
{
   login_streak();

   //
   // Add more stuff here if you like, like competitions
   //

   return;
}
dilend


// 1 means your first login
// 2 means you've logged on for two consecutive days
// 3... etc.
//
dilbegin streak_bonus(nCount : integer);
external
	integer add_bp@bounty(n : integer, u : unitptr);
var
	bp : integer;
code
{
	log("Awarding " + self.symname + " streak bonus for " + itoa(nCount) + " days.");
	
	bp := add_bp(nCount,self);

   // Either cap at e.g. 7 days, or just keep it going up?
   // for now, we'll let it run! :)
   return;
}
dilend


// Call when a PC logs on or off (self == pc)
//
// .[1] == Last day the streak was hit
// .[2] == streak since this day
//
dilbegin login_streak();
external
   streak_bonus(nCount : integer);

var
   exd : extraptr;
   sl  : stringlist;
   nToday : integer;
   nLast  : integer;
   nCount : integer;
code
{
   exd := "$loginstreak" in self.info;
   if (exd == null)
   {
      sl := {"$loginstreak", "0", "0"};
      addextra(self.info, sl, "");
      exd := "$loginstreak" in self.info;
   }

   nToday := realtime / (60*60*24);
   nLast  := atoi(exd.names.[1]);
   nCount := nToday - atoi(exd.names.[2]) + 1;

   // did we do a login streak?
   if (nLast+1 == nToday)
   {
      act("<div class='cpy'>Your login streak is now at "+itoa(nCount)+" days.</div>", A_ALWAYS, self, null, null, TO_CHAR);
      act("<div class='xpgain'>You gain "+itoa(nCount)+" bounty point(s) as a login streak reward.</div>", A_ALWAYS, self, null, null, TO_CHAR);
      exd.names.[1] := itoa(nToday);  // Move to the next day
      streak_bonus(nCount);
   }
   else if (nLast+1 < nToday)    // Did we miss out on the streak?
   {
      act("<div class='cpy'>Your login streak is now at "+itoa(nCount)+" days.</div>", A_ALWAYS, self, null, null, TO_CHAR);
      act("<div class='xpgain'>You gain "+itoa(nCount)+" bounty point(s) as a login streak reward.</div>", A_ALWAYS, self, null, null, TO_CHAR);
      exd.names.[1] := itoa(nToday);
      exd.names.[2] := itoa(nToday);  // Set streak since
      streak_bonus(1);
   }
   else
   {
      // log("A day hasn't passed yet....");
      return;
   }

   return;
}
dilend

dilbegin delivery();
external
	integer add_bp@bounty(n : integer, u : unitptr);
var
	pc : unitptr;
	recip : unitptr;
	package : unitptr;
	iFight : integer;
	quest : extraptr;
	minlev : integer;
	maxlev : integer;
	qnames : stringlist;
	rtitle : string;
	rdesc : string;
	rlev : integer;
	temp : integer;
	i : integer;
code
{
	heartbeat:=PULSE_SEC * 5;
	on_activation(self.position == POSITION_FIGHTING, ina_fight);
	package:=null;

:init:
	iFight := interrupt(SFB_COM, self.position==POSITION_FIGHTING, ina_fight);

:start:
	nopriority;
	wait(SFB_DONE, (activator.type==UNIT_ST_PC) and (activator.outside==self.outside) and (command("say")) and (argument=="i need work"));
	priority;
	pc:=activator;
	secure(pc, lostpc);

	quest:="packagefor" in pc.quests;
	if(quest!=null)
	{
		exec("say You have not yet delivered the package I gave you!", self);
		exec("say I will clear you of the responsibility (and the quest flag),", self);
		exec("say but you will be penalized and lose 300 xp and 2 bounty points.", self);
		exec("say Nod if this is acceptable.", self);
		wait(SFB_DONE, (pc.outside!=self.outside) or ((activator==pc) and (command("nod"))));
		if(pc.outside!=self.outside) goto lostpc;
		temp:=dildestroy("packagefor@bounty", pc);
		subextra(pc.quests, "packagefor");
		experience(-300, pc);
		i := add_bp(-2,pc);
		act("<div class='xpgain'>You lose 300 xp and 2 bounty points.</div>", A_SOMEONE, pc, null, null, TO_CHAR);
		unsecure(pc);
		goto start;
	}

	minlev:=pc.level+(pc.level/10);
	if(pc.level==50) maxlev:=199;
	else maxlev:=pc.level+(pc.level/5);

:loop:
	recip:=findrndunit(pc, FIND_UNIT_WORLD, UNIT_ST_NPC);
	secure(recip, loop);
	rdesc:=recip.outside_descr;
	rtitle:=recip.title;
	rlev:=recip.level;
	unsecure(recip);
	if(((rlev<minlev) or (rlev>maxlev)) and (pc.level<GOD_LEVEL)) goto loop;

	exec("say Oh you do eh? Fine, "+pc.name+"!", self);
	exec("say I need you to deliver a package for me.", self);
	exec("say Just nod if you will.", self);

	wait(SFB_DONE, (pc.outside!=self.outside) or ((activator==pc) and (command("nod"))));
	if(pc.outside!=self.outside) goto lostpc;

	package:=load("package@bounty");
	package.title:="a package for "+rtitle;
	package.outside_descr:="A package for "+rtitle+" lies here.";
	addstring(package.names, rtitle);
	qnames:=null;
	addstring(qnames, "Package for "+rtitle);
	addstring(qnames, "packagefor");
	addstring(qnames, rtitle);
	addextra(pc.quests, qnames, "");
	if(not dilfind("packagefor@bounty", pc)) dilcopy("packagefor@bounty("+itoa(rlev)+")", pc);
	exec("say OK the package is for "+rtitle+".", self);
	exec("give packagefor "+pc.name, self);
	exec("drop packagefor", self);
	exec("say Don't you dare open that, it's not for you!", self);pause;
	exec("say The recipient you are looking for looks like this:", self);
	exec("say "+rdesc, self);
	exec("say And don't worry about giving it to the wrong recipient,", self);
	exec("say if you get it wrong it will be returned to you.", self);pause;
	exec("say If you deliver the package successfully you will get "+itoa(50*rlev)+" xp and 1 bounty point.", self);
	exec("say Now hurry!  That's an important package!", self);
	unsecure(pc);
	goto start;

:ina_fight:
	clear(iFight);
	while(self.position==POSITION_FIGHTING) pause;
	goto init;

:lostpc:
	exec("say Fine! Be that way! Next please?", self);
	unsecure(pc);
	goto start;
}
dilend

dilbegin package();
var
	pc : unitptr;
code
{
	heartbeat:=PULSE_SEC * 4;
:start:
wait (SFB_DONE, (command("open")) and
(self==target));
	block;
	pc := activator;
    secure(pc,lostpc);
	act("$1N opens $2n and it explodes in $1s face, injuring $1m greatly!",
    A_SOMEONE, pc, self, null, TO_REST);
	act("You open $2n and it explodes in your face, injuring you greatly!",
    A_SOMEONE, pc, self, null, TO_CHAR);
	if(pc.hp>0) pc.hp := 0;
	position_update(pc);
	unsecure(pc);
:lostpc:
	destroy(self);
}
dilend

dilbegin aware packagefor(rlev : integer);
external
	integer add_bp@bounty(n : integer, u : unitptr);
var
	q1 : extraptr;
	q2 : extraptr;
	temp : integer;
code
{
	heartbeat:=PULSE_SEC * 4;
:start:
	wait(SFB_DONE, (command("give") and (activator==self) and ("packagefor" in medium.names) and (target.type==UNIT_ST_NPC)));
	q1:="packagefor" in activator.quests;
	q2:=target.title in activator.quests;
	if((q1==q2) and (q1!=null) and (target.title in medium.names))
	{
		subextra(activator.quests, "packagefor");
		
		experience((50*rlev), activator);
		temp := add_bp(1, activator); 
		exec("say Thanks, I've been waiting for this, "+activator.name+"!", target);
		act("<div class='xpgain'>You gain "+itoa(50*rlev)+" xp and 1 bounty point.</div>", A_SOMEONE, activator, null, null, TO_CHAR);
		temp := transfermoney(null,activator,((50*rlev)/2));	
		destroy(medium);
		temp := dildestroy("packagefor@bounty", self);
		goto start;
	}
	else
	{
		exec("say Thanks, but I don't need this.  You keep it.", target);
		exec("give packagefor "+activator.name, target);
		exec("drop packagefor", target);
		goto start;
	}
}
dilend

dilbegin anvils();
var
	i : integer;
	j : integer;
	sl : stringlist;
code
{
	sl := {"The sound of hammers on anvils rings through the smithy.",
			"A large burp of flames suddenly erupts from the furnace.",
			"You suddenly notice an apprentice peering your way.",
			"The Blacksmith takes a long look in your direction, before returning to his work.",
			"A young groll lumbers to the bellows, and begins pumping them.",
			"From somewhere outside, the sound of laughter finds its way through the din.",
			"The Blacksmith turns and begins the task of puring the smelter's contents into forms.",
			"Two apparentices appear and wheel a barrow to a large pile of ore and start shoveling.",
			"The heat from the furnace washes over you.",
			"The din of activity has escalated to a dull roar."};

:start:
			
	i := rnd(0,length(sl)-1);
	j := rnd(3,5);
	
	heartbeat := PULSE_ZONE * j;
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act (sl.[i], A_SOMEONE, self, null, null, TO_ALL);
	goto start;
	
}
dilend

dilbegin tablet();
var
	pc : unitptr;
	slt : stringlist;
	bty : extraptr;
	targ : unitptr;
	sdate : string;
	buf : string;
	which : string;
	slbty : stringlist;
	i : integer;
	cstat : string;
code
{
	slbty := {"$daily_target","$monthly_target","$bonus_target"};
:start:
	
	wait(SFB_DONE,command("look") and ((argument == "bounty hunters tablet") or (argument == "tablet")) and (activator == self.outside));
	
	pc := activator;
	secure(pc,lostpc);
	
	block;
	buf := "<div class='cr'>REWARD!!</div><br><br>";
	i := 0;
	
	while (i < length(slbty))
	{
		bty := slbty.[i] in pc.extra;
		if (bty)
		{
			slt := split(bty.descr,"|");
			if("DONE" in slt) 
				cstat := "Closed";
			else
				cstat := "Open";
			which := replace("$","",slbty.[i]);
			which := replace("_target","",which);
			targ := findsymbolic(slt.[1]);
			sdate := slt.[0];
			buf := buf + 
			"<div class='cr'>-------------------------------------------------------</div>" +
			"<br><div class='cpw'>Contract Date: </div><div class='cr'>" + sdate +
			"<br><div class='cpw'>Contract Term: </div><div class='cr'>" + which +
			"<br><div class='cpw'>Target Name: </div><div class='cr'>" + targ.name + 
			"<br><div class='cpw'>Target Alias: </div><div class='cr'>" + targ.title + 
			"<br><div class='cpw'>Target Description: </div><div class='cr'> " + targ.outside_descr +
			"<br><div class='cpw'>Contract Status: </div><div class='cr'> " + cstat +
			"<br>-------------------------------------------------------</div><br><br>";
		}
		i := i + 1;
	}
	
	act (buf, A_ALWAYS, pc, null, null, TO_CHAR);
	
:lostpc:

	unsecure(pc);
	goto start;
	
}
dilend

dilbegin reprint(pc:unitptr, buf:string, which:string);
var
	slt : stringlist;
	bty : unitptr;
	targ : unitptr;
	sdate : string;
code
{
	log("Reprint called for "+which+" contract: "+buf);
	slt := split(buf,"|");
	targ := findsymbolic(slt.[1]);
	sdate := slt.[0];
	bty := load("contract@bounty");
	buf := bty.extra.[0].descr + 
	"<br><div class='cpw'>Contract Date: </div><div class='cr'>" + sdate +
	"<br><div class='cpw'>Contract Term: </div><div class='cr'>" + which +
	"<br><div class='cpw'>Target Name: </div><div class='cr'>" + targ.name + 
	"<br><div class='cpw'>Target Alias: </div><div class='cr'>" + targ.title + 
	"<br><div class='cpw'>Target Description: </div><div class='cr'> " + targ.outside_descr;
	
	bty.extra.[0].descr := buf;
	act (buf, A_ALWAYS, pc, null, null, TO_ALL);	
	link(bty,pc);
	return;
}
dilend

dilbegin status();
external
	reprint@bounty(pc:unitptr, buf:string, which:string);
var
	pc 		: unitptr;
	daily	: extraptr;
	monthly : extraptr;
	bonus 	: extraptr;
	i		: integer;
	j 		: integer;
	k 		: integer;
	arg		: string;
	bty 	: unitptr;
	targ 	: unitptr;
	slt		: stringlist;
	sm,sd,sb : string;
code
{
	heartbeat:= PULSE_SEC;

:start:

	wait(SFB_CMD,command("say") and ("contract status" == argument));

	pc := activator;
	secure(pc,donezo);
	
	daily := "$daily_target" in pc.extra;
	monthly := "$monthly_target" in pc.extra;
	bonus := "$bonus_target" in pc.extra;
	
	i := 0;
	j := 0;
	k := 0;
	
	if(daily != null)
	{
		sd := daily.descr;
		i := i + 1;
		if("DONE" in sd)
			j := j + 1;
		else
			k := k + 1;
	}
	if(monthly != null)
	{
		sm := monthly.descr;
		i := i + 1;
		if("DONE" in sm)
			j := j + 1;
		else
			k := k + 1;
	}
	if(bonus != null)
	{
		sb := bonus.descr;
		i := i + 1;
		if("DONE" in sb)
			j := j + 1;
		else
			k := k + 1;
	}
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say You currently have "+itoa(i)+" contracts.",self);
	if(k > 0)
		exec("say Of those, you have "+itoa(k)+" open, and "+itoa(j)+" closed.",self);
	else 
	{
		exec("say You have closed all of your contracts.",self);
		goto start;
	}
	
:try_again:
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Would you like details on your open contracts?",self);
	exec("say Nod for yes, or shake for no.",self);

	wait(SFB_CMD,(pc == activator));
	if (command("nod"))
		goto sure_thing; 
	else if (command("shake"))
		goto donezo;
	else
		goto try_again;
	
:sure_thing:

	if(k > 0)
	{
		wait(SFB_TICK,command(CMD_AUTO_TICK));
		exec("say Here's what you have open, "+pc.name+".",self);
		if((sb != null) and (not("DONE" in sb)))
			reprint(pc,sb,"bonus");
		if((sd != null) and (not("DONE" in sd)))
			reprint(pc,sd,"daily");
		if((sm != null) and (not("DONE" in sm)))
			reprint(pc,sm,"monthly");
		
		if(k == 1)
		{
			act ("$2n quietly slips you a contract.", A_SOMEONE, pc, self, null, TO_CHAR);
			act ("$2n quietly slips $1n something.", A_SOMEONE, pc, self, null, TO_REST);
		}
		else
		{
			act ("$2n quietly slips you your contracts.", A_SOMEONE, pc, self, null, TO_CHAR);
			act ("$2n quietly slips $1n something.", A_SOMEONE, pc, self, null, TO_REST);
		}
	}

:donezo:
	unsecure(pc);
	goto start;
}
dilend

dilbegin join();
var
  pc: unitptr;
  exdp: extraptr;

code
{
	heartbeat:= PULSE_SEC*4;

:start:
	wait(SFB_CMD,(activator.type == UNIT_ST_PC) and (not(JOIN_ONGOING in activator.quests)) and (not(JOIN_COMPLETE in activator.quests)) and (activator.position == POSITION_STANDING));

	
	pc := activator;
	secure(pc, lostpc);
	exdp := "$daily_max" in pc.extra;
	if((atoi(exdp.descr) < 30) or (exdp == null))
		goto start;
	
    wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("hmm",self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
    exec("say Perhaps, you could be worthy.", self);
    wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say It is no small feat to close 30 consecutive daily contracts.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say It shows a certain commitment to our way.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say For a hundred generations we've filled this role.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say There have been times of harship, but the guild has maintained an unbroken line.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("emote looks at you long and hard.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say I wonder.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Do you think yourself up to the task of joining our ranks?", self);

:accept_rogue:

	wait(SFB_CMD, activator == pc);
	if (command("nod"))
		goto rogue_accepted; 
	else if (command("shake"))
		goto no_quest;
	else
	{
		exec("say Nod your head if you think yourself up to the challenge.", self);
		goto accept_rogue;
	}

:no_quest:

	exec("say So be it!", self);
	unsecure(pc);
	goto start;

:rogue_accepted:

	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say The journey will be long and challenging.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say You will have to prove yourself time and again to the guild.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say I can't tell you much more, only that this way of life will change you.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Go now, and pray at the Hunters Shrine.", self);
	addextra(pc.quests, {JOIN_ONGOING}, "");

	unsecure(pc);
	goto start;	
:lostpc:

	goto start;
}
dilend // END join


dilbegin pray();
var
  pc : unitptr;
  exdp : extraptr;
  tt : integer;

code
{
	heartbeat:= PULSE_SEC*4;

:start:
	wait(SFB_CMD, command("pray") and ((JOIN_ONGOING in activator.quests) or (JOIN_COMPLETE in activator.quests)));
	
	block;
	pc := activator;
	secure(pc,lostpc);
	
	act("You kneel and begin to pray.",A_ALWAYS, pc, null, null, TO_CHAR); 
	act("$1n kneels and begins to pray.",A_SOMEONE, pc, null, null, TO_REST);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
   tt:=atoi(self.extra.["$PK_TIMER"].descr);
	if(not(realtime-tt>600))
	{
		act("As you pray, a sense that recent battles prevent focus.",A_ALWAYS, pc, null, null, TO_CHAR);
		goto lostpc;
	}
	act("As you pray, a sense of purpose builds within you.",A_ALWAYS, pc, null, null, TO_CHAR); 	
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act("The light of the candles slowly stops its flickering, and steadies.",A_ALWAYS, pc, null, null, TO_ALL); 
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act("The light begins to intensify, the formerly dark room is now bright as day.",A_ALWAYS, pc, null, null, TO_ALL);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act("The world around you begins to dissolve.",A_ALWAYS, pc, null, null, TO_CHAR); 
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act("There is a <div class='cpy'>bright flash</div>!",A_ALWAYS, pc, null, null, TO_ALL);
	link(pc,findroom("sanctum@bounty"));
	exec("look",pc);
	act("The candle light returns to its flickering.",A_ALWAYS, pc, null, null, TO_ROOM); 
	
:lostpc:
	unsecure(pc);
	goto start;
}
dilend // END Pray

dilbegin pray2();
var
  pc: unitptr;
  exdp: extraptr;

code
{
	heartbeat := PULSE_SEC*2;

:start:
	wait(SFB_CMD, command("pray") and ((JOIN_ONGOING in activator.quests) or (JOIN_COMPLETE in activator.quests)));
	block;
	pc := activator;
	secure(pc,lostpc);
	act("You kneel and begin to pray.",A_ALWAYS, pc, null, null, TO_CHAR); 
	act("$1n kneels and begins to pray.",A_SOMEONE, pc, null, null, TO_REST);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act("There is a <div class='cpw'>cold draft</div>.",A_ALWAYS, pc, null, null, TO_ALL);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act("The world around you dissolves.",A_ALWAYS, pc, null, null, TO_CHAR); 
	link(pc,findroom("shrine@bounty"));
	exec("look",pc);
	act("The room returns to normal.",A_ALWAYS, pc, null, null, TO_ROOM); 
:lostpc:
	unsecure(pc);
	goto start;
}
dilend // END Pray2

dilbegin feast();
var
	pc : unitptr;
	u : unitptr;
code
{
	heartbeat:=PULSE_SEC*6;
	
:start:
	
	wait(SFB_CMD, command("feast") and ((JOIN_ONGOING in activator.quests) or (JOIN_COMPLETE in activator.quests)));

	block;
	pc:= activator;
	secure(pc,lostpc);
	
	act("You sit and partake in the hunters feast.",A_ALWAYS, pc, null, null, TO_CHAR);
	act("$1n sits and partakes in the hunters feast.",A_ALWAYS, pc, null, null, TO_REST);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	if(not("$feasted" in pc.extra))
	{
		act("You have an uneasy feeling that you're being watched.",A_ALWAYS, pc, null, null, TO_CHAR);
		wait(SFB_TICK,command(CMD_AUTO_TICK));
		act("As you continue eating the feeling grows stronger.",A_ALWAYS, pc, null, null, TO_CHAR);
		wait(SFB_TICK,command(CMD_AUTO_TICK));
		act("You suddenly realize that you are not alone.",A_ALWAYS, pc, null, null, TO_CHAR);
		u := load("guide@bounty");
		goto lostpc;
	}
	act("You finish eating, and take a moment to rest.",A_ALWAYS, pc, null, null, TO_CHAR);
	exec("rest",pc);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	pc.hp := pc.max_hp;
	pc.endurance := pc.max_endurance;
	pc.mana := pc.max_mana;
	pc.full:=24;
	pc.thirst:=24;
	act("You feel fully restored.",A_ALWAYS, pc, null, null, TO_CHAR);
	
	:lostpc:
	unsecure(pc);
	goto start;
}
dilend // END Feast

dilbegin expel();
var
	pc:unitptr;
code
{
	heartbeat := PULSE_SEC;
	
:start:
	
	wait(SFB_PRE, command(CMD_AUTO_DAMAGE) and (target == self));

	pc:= activator;
	secure(pc,lostpc);
	
	exec("say It is a special stype of fool who would attack me here.",self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	act("$1n gestures, and the world fades.",A_SOMEONE, self, pc, null, TO_CHAR);
	act("$1n gestures, and $2n begins to fade.",A_SOMEONE, self, pc, null, TO_ROOM);
	pc.hp := 0;
	pc.endurance := 0;
	pc.mana := 0;
	subextra(pc.extra, "$daily_streak");
	position_update(pc);
	link(pc,findroom("entry_room@bounty"));
	act("You feel a searing pain!",A_SOMEONE, self, pc, null, TO_CHAR);
:lostpc:
	unsecure(pc);
	goto start;
}
dilend // END Expel

dilbegin guide();
var
	pc:unitptr;
code
{
	heartbeat:=PULSE_SEC*4;
	
:start:
	
	wait(SFB_CMD, (activator.type == UNIT_ST_PC) and (JOIN_ONGOING in activator.quests) and (not("$feasted" in activator.extra)));

	pc:= activator;
	secure(pc,lostpc);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Please, dont let me interrupt you partaking of this feast.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say After all, it was prepared just for your arrival.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say I do however have... a question for you.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say The Bounty Hunters have been here for a period of time which extends beyond our records.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Indeed, The Historian believes this collective has existed since before ANY written records.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say I'll be frank, "+pc.name+". Our guild is at risk.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Our way of living is at risk.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say A great enemy is preparing to unleash its fury upon us.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say While we have worked at the services of emperors, kings, demons and angels, a new force" +
		" which considers itself the face of a just world is rising.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say While we have never operated in the shadows, and certainly consider our role one that works" +
		" to bring balance in the world, some believe we need to be destroyed.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("ponder",self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Trust is hard to come by, but I believe you can be relied on.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Go and speak to The Historian. He can give you further guidance.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say One final note - you may leave here and return at any time. Resume your prayers in the Inner Sanctum, and you will find yourself returned to the world outside.", self);
	pc.hp := pc.max_hp;
	pc.endurance := pc.max_endurance;
	pc.mana := pc.max_mana;
	pc.full:=24;
	pc.thirst:=24;
	act("As you finish the feast, you feel fully restored.",A_ALWAYS, pc, null, null, TO_CHAR);
	addextra(pc.extra,{"$feasted"},"0");
	position_update(pc);
	act("You suddenly notice that The Guide is no longer here.",A_ALWAYS, pc, null, null, TO_ALL);
	destroy(self);
	
:lostpc:

	unsecure(pc);
	goto start;
}
dilend // END Guide

dilbegin historian();
var
	pc:unitptr;
code
{
	heartbeat:=PULSE_SEC*4;
	
:start:
	
	wait(SFB_CMD, (activator.type == UNIT_ST_PC) and (JOIN_ONGOING in activator.quests) and ("$feasted" in activator.extra) and (not("$historian_ongoing" in activator.extra)));

	pc:= activator;
	secure(pc,lostpc);
	
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Welcome "+pc.name+".", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say I hope The Guide made you feel welcome?", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say There was considerable preparation put into your arrival.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say I'll not look to deceive you, the Guild is in need of your help.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say I've been here keeping these timeless records for years beyond count.", self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	addextra(pc.extra,{"$historian_ongoing"},"0");
	
:lostpc:

	unsecure(pc);
	goto start;

}
dilend // END historian


dilbegin recall spree_f(dur : integer);
var
	st : integer;
code
{
	
	heartbeat := PULSE_SEC*10;
	subextra(self.extra,"$SPREE_TIMER");
	addextra(self.extra,{"$SPREE_TIMER"},itoa(realtime));
	
:start:
	
	wait(SFB_DEAD|SFB_TICK, (command(CMD_AUTO_DEATH) and (activator == self.fighting)) or (command(CMD_AUTO_TICK)));
	
	if(command(CMD_AUTO_DEATH))
		send("kill");
	if(command(CMD_AUTO_TICK))
	{
		st := atoi(self.extra.["$SPREE_TIMER"].descr);
		if(realtime - st > ((dur + 1) * 60))
		{
			subextra(self.extra,"$spree_streak");
			addextra(self.extra,{"$spree_streak"},itoa(0));
			self.extra.["$DAILY_SPREE"].descr := replace("ongoing","FAIL",self.extra.["$DAILY_SPREE"].descr); 
			quit;
		}
	}
		
	goto start;
}
dilend // END spree_f

// duration is number of minutes, amt is amount of xp in thousands, 
// n is the number of kills, and stype is "duration","xp","kills", or a 
// combination in the format "duration|xp|kills"
// this function is for followers, and sends results to the group master
dilbegin aware recall spree_m(dur : integer, amt : integer, n : integer, stype : integer);
external
	string stringdate@bounty (n : integer);
var
	st : integer; // spree time
	sc : integer; // spree count
	sx : integer; // spree xp
	bx : integer; // baseline xp
	kn : integer; // kills needed
	xn : integer; // xp needed
	seconds : integer;
	minutes : integer;
	i : integer;
	u : unitptr;
	arg : string;
	sdate 	: string;
	sl : stringlist;
code
{
:init:

	subextra(self.extra,"$SPREE_TIMER");
	addextra(self.extra,{"$SPREE_TIMER"},itoa(realtime));

	bx := self.exp;
	sc := 0;
	
	heartbeat := PULSE_SEC*10;
	sdate := stringdate(3);
	
:start:
	
	arg := "tick";
	
	wait(SFB_MSG|SFB_TICK,(command(CMD_AUTO_MSG) and (argument == "kill")) or (command(CMD_AUTO_TICK)));
	
	if(argument == "kill")
		arg := argument;
	
	st := atoi(self.extra.["$SPREE_TIMER"].descr);

	
	if(realtime - st > (dur * 60))
		goto donezo;
	if(arg == "tick")
		goto start;
	
	sc := sc + 1;
	sx := self.exp - bx;
	
	foreach(UNIT_ST_PC,u)
	{
		sl := split(u.extra.["$DAILY_SPREE"].descr,"|");
		if(((u.master == self) or (u == self)) and dilfind("spree_f@bounty",u) and (sl.[0] == sdate) and (sl.[1] == "ongoing") and (sl.[2] == self.name))
		{
			kn := n - sc;
			if(kn < 0)
				kn := 0;
			xn := (amt * 1000)- sx;
			if(xn < 0)
				xn := 0;
			sendtext ("You need "+itoa(kn)+" kills and "+itoa(xn)+" experience to complete your spree.<br/>",u);
			
		:check_time:

			seconds := realtime - st;
			seconds := (60 * dur) - seconds;
			minutes := seconds / 60;
			
			if (minutes < 1)
			{
				if (seconds == 1)
					sendtext ("You have 1 more second before your spree ends.<br/>",u);
				else
					sendtext ("You have "+itoa(seconds)+" more seconds before your spree ends.<br/>",u);
			}
			else
			{
				seconds := seconds - (minutes * 60);
				if ((minutes == 1) and (seconds == 1))
					sendtext ("You have 1 minute and 1 second before your spree ends.<br/>",u);
				else if (minutes == 1)
					sendtext ("You have 1 minute and "+itoa(seconds)+" seconds before your spree ends.<br/>",u);
				else if (seconds == 1)
					sendtext ("You have "+itoa(minutes)+" minutes and 1 second before your spree ends.<br/>",u);
				else
					sendtext ("You have "+itoa(minutes)+" minutes and "+itoa(seconds)+" seconds before your spree ends.<br/>",u);
			}
		}
	}
	goto start;
	
:donezo:

	kn := n - sc;
	if(kn < 0)
		kn := 0;
	xn := (amt * 1000) - sx;
	if(xn < 0)
		xn := 0;
	
	sl := split(self.extra.["$DAILY_SPREE"].descr,"|");
	if((kn == 0) and (xn == 0))
	{
		log("Spree Tracker: "+self.name+" has lead a successful spree of type "+sl.[3]+" with "+itoa(sc)+"/"+itoa(n)+" kills and "+itoa(sx)+"/"+itoa(amt)+"000 XP in "+itoa(dur - 1 - minutes)+":"+itoa(60 - seconds)+". Duration was "+itoa(dur)+":00.");
		u := self;
		while (u.type==UNIT_ST_PC)
		{
			sendtext(self.name+" has just lead a successful spree with "+itoa(sc)+"/"+itoa(n)+" kills and "+itoa(sx)+"/"+itoa(amt)+"000 XP in "+itoa(dur - 1 - minutes)+":"+itoa(60 - seconds)+". Duration was "+itoa(dur)+":00.",u);
			u := u.gnext;
		}
	}
	else
		log("Spree Tracker: "+self.name+" failed to lead a spree of type "+sl.[3]+" with "+itoa(sc)+"/"+itoa(n)+" kills and "+itoa(sx)+"/"+itoa(amt)+"000 XP. Duration was "+itoa(dur)+":00.");
	
	foreach(UNIT_ST_PC,u)
	{
		if("$DAILY_SPREE" in u.extra)
		{
			sl := split(u.extra.["$DAILY_SPREE"].descr,"|");
			if(((u.master == self) or (u == self)) and (sl.[0] == sdate) and (sl.[1] == "ongoing") and (sl.[2] == self.name))
			{	
				
				subextra(u.extra,"$SPREE_XP");
				addextra(u.extra,{"$SPREE_XP"},itoa(sx));
				subextra(u.extra,"$SPREE_COUNT");
				addextra(u.extra,{"$SPREE_COUNT"},itoa(sc));
				i := dildestroy("spree_f@bounty",u);
				act("<div class='cpr'>Your spree has come to an end! You bagged "+itoa(sc)+" kills and gained "+itoa(sx)+" experience.</div>",A_ALWAYS, u, null, null, TO_CHAR);
				if((kn == 0) and (xn == 0))
					u.extra.["$DAILY_SPREE"].descr := replace("ongoing","SUCCESS",u.extra.["$DAILY_SPREE"].descr); 
				else
				{
					subextra(u.extra,"$spree_streak");
					addextra(u.extra,{"$spree_streak"},itoa(0));
					u.extra.["$DAILY_SPREE"].descr := replace("ongoing","FAIL",u.extra.["$DAILY_SPREE"].descr); 
				}
			}
		}
	}
	quit;
}
dilend // END spree_m


dilbegin new_spree();
external
	integer dateformat@bounty (s : string);
	string stringdate@bounty (n : integer);
var
	pc 		: unitptr;
	i 		: integer;
	dur 	: integer;
	amt 	: integer;
	n 		: integer;
	stype 	: integer;
	u 		: unitptr;
	tot 	: integer;
	arg 	: string;
	sdate 	: string;
	ssdate 	: string;
	idate 	: integer;
	isdate 	: integer;
	exdp 	: extraptr;
	sl		: stringlist;
	
code
{
:start:
	
	heartbeat := PULSE_SEC;
	wait(SFB_CMD, (command("say") and (("start spree" == argument) or ("spree" == argument) or ("killing spree" == argument)) and (activator.type == UNIT_ST_PC)));
		
	arg := argument;	
	
	pc := activator;
	secure(pc,donezo);
	
	sdate := stringdate(3);
	
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exdp := "$daily_total" in pc.extra;
	if(exdp)
		tot := atoi(exdp.descr);
	exdp := "$monthly_total" in pc.extra;
	if(exdp)
		tot := tot + atoi(exdp.descr);
	exdp := "$bonus_total" in pc.extra;
	if(exdp)
		tot := tot + atoi(exdp.descr);
		
	if(self.outside.symname != "smithy@bounty")
	{
		exec("say Alas, I must return to my Smithy first.",self);
		goto donezo;
	}
	if(tot < 20)
	{
		exec("say You're yet to prove your readiness, "+pc.name+"!",self);
		wait(SFB_TICK,command(CMD_AUTO_TICK));
		exec("say Work some more bounties with the Hunter, and then come back to see me.",self);
		goto donezo;
	}	
	if(pc.master.master != pc)
	{
		exec("say Only a group leader can start a new killing spree!",self);
		goto donezo;
	}
	if("$DAILY_SPREE" in pc.extra)
	{
		sl := split(pc.extra.["$DAILY_SPREE"].descr,"|");
		if((sdate == sl.[0]) and (sl.[1] == "SUCCESS"))
		{
			exec("say You have already closed a killing spree today!",self);
			goto donezo;	
		}		
		if((sdate == sl.[0]) and (sl.[1] == "ongoing"))
		{
			exec("say Finish the spree you are on before starting a new one!",self);
			goto donezo;	
		}
	}
	if(arg == "start spree")
		goto killem;
	
	heartbeat := PULSE_SEC*3;
	exec("emote looks up from his work.",self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say So "+pc.name+", you'd like to help the Guild with a need?",self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say This will be a task well beyond simply taking a bounty.",self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Ensure that you and your allies have prepared for what is to come.",self);
	
:loop:

	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("say Nod your head to continue, otherwise shake.",self);

	wait(SFB_CMD, (activator == pc));
	
	if(command("nod"))
		goto killem;
	else if(command("shake"))
		goto donezo;
	else
		goto loop;

:killem:

	i := rnd(0,3);
	if(not("$spree_date" in self.outside.extra))
	{	
		addextra(self.outside.extra,{"$spree_date"},sdate);
		addextra(self.outside.extra,{"$spree_type"},itoa(i));
	}
	else 
	{
		ssdate := self.outside.extra.["$spree_date"].descr;
		idate := dateformat@bounty(sdate);
		isdate := dateformat@bounty(ssdate);
		if(idate > isdate)
		{
			subextra(self.outside.extra,"$spree_date");
			subextra(self.outside.extra,"$spree_type");
			addextra(self.outside.extra,{"$spree_date"},sdate);
			addextra(self.outside.extra,{"$spree_type"},itoa(i));
		}
	}
	
	stype := atoi(self.outside.extra.["$spree_type"].descr);
	
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	if ((sdate == sl.[0]) and (sl.[1] == "FAIL"))
	{
		exec("say Perhaps you'll do a bit better this time, "+pc.name+".",self);	
	}
	else
		exec("say So you're ready to fight for the Guild?",self);
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	
	// these are the different streak types that can be utilized. Each has its own reward structure.
	if(stype == 0) 
	{
		dur := rnd(10,15);
		amt := dur * 4;
		n := dur * 8;
		exec("say This will be a "+itoa(dur)+" minute spree requiring "+itoa(n)+" kills and "+itoa(amt)+",000 xp.",self);
	}
	if(stype == 1)
	{
		dur := rnd(20,30);
		amt := dur * 3;
		n := dur * 7;
		exec("say This will be a "+itoa(dur)+" minute spree requiring "+itoa(n)+" kills and "+itoa(amt)+",000 xp.",self);
	}
	if(stype == 2)
	{
		dur := rnd(4,6);
		amt := 0;
		n := 0;
		exec("say This will be a "+itoa(dur)+" minute spree with no kill or xp minimums.",self);
	}
	if(stype == 3)
	{
		dur := rnd(10,30);
		amt := 0;
		n := dur * 12;
		exec("say This will be a "+itoa(dur)+" minute spree requiring "+itoa(n)+" kills and no xp minimum.",self);
	}	
	dilcopy ("spree_m@bounty("+itoa(dur)+","+itoa(amt)+","+itoa(n)+","+itoa(stype)+")",pc);
	
	
	heartbeat := PULSE_SEC;
	i := 5;
	while(i >= 0)
	{		
		foreach(UNIT_ST_PC,u)
		{
			if(not("$DAILY_SPREE" in u.extra))
				addextra(u.extra,{"$DAILY_SPREE"},sdate+"|blank|blank|blank");
			sl := split(u.extra.["$DAILY_SPREE"].descr,"|");
			if(((u.master == pc) or (u == pc)) and (not(dilfind("spree_f@bounty", u))) and (not((sl.[0] == sdate) and ((sl.[1] == "CLOSED") or (sl.[1] == "ongoing")))))
			{
				if(i == 0)
				{	
					act("<div class='cpg'>== GO! ==</div>",A_ALWAYS, u, null, null, TO_CHAR);
					subextra(u.extra,"$DAILY_SPREE");
					addextra(u.extra,{"$DAILY_SPREE"},sdate+"|ongoing|"+pc.name+"|"+itoa(stype));
					dilcopy ("spree_f@bounty("+itoa(dur)+")",u);
					act("<div class='cpr'>You have been enrolled in a new killing spree!<div>",A_ALWAYS, u, null, null, TO_CHAR);
				}
				else
					act("<div class='cpg'>== "+itoa(i)+" ==</div>",A_ALWAYS, u, null, null, TO_CHAR);
			}
		}
		wait(SFB_TICK,command(CMD_AUTO_TICK));
		i := i - 1;
	}
	
:donezo:
	
	unsecure(pc);
	goto start;	
}
dilend // END new_spree

dilbegin end_spree();
external
	string stringdate@bounty(n : integer);
	integer add_bp@bounty(n : integer, u : unitptr);
var
	pc : unitptr;
	i : integer;
	u : unitptr;
	s : string;
	sdate : string;
	idate : integer;
	sl : stringlist;
	rwrd : integer;
	bp : integer;
	sc : integer;
	sx : integer;
code
{
	heartbeat := PULSE_SEC;
	
:start:
	
	wait(SFB_CMD, (activator.type == UNIT_ST_PC) and (command("say") and ("end spree" == argument)));

	pc := activator;
	secure(pc,donezo);
	
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	
	if("$DAILY_SPREE" in pc.extra)
	{
		s := pc.extra.["$DAILY_SPREE"].descr;
		sl := split(s,"|");
		sdate := stringdate(3);
		if(sdate == sl.[0])
		{
			if("FAIL" == sl.[1])
			{
				exec("say I'm sorry, you failed to meet the conditions in time today "+pc.name,self);
			}
			else if ("SUCCESS" == sl.[1])
			{
				exec("say You've done it "+pc.name+"!",self);
				subextra(pc.extra,"$DAILY_SPREE");
				addextra(pc.extra,{"$DAILY_SPREE"},sdate+"|CLOSED");
				if("$spree_streak" in pc.extra)
				{
					i := atoi(pc.extra.["$spree_streak"].descr) + 1;
					pc.extra.["$spree_streak"].descr := itoa(i);
				}
				else
				{
					i := 1;
					addextra(pc.extra,{"$spree_streak"},itoa(i));
				}
				sx := atoi(pc.extra.["$SPREE_XP"].descr);
				sc := atoi(pc.extra.["$SPREE_COUNT"].descr);
				rwrd := 5000;
				bp := 5;
				if(sl.[3] == "0")
				{
					rwrd := rwrd * 6;
					bp := bp + 3;
				}
				else if(sl.[3] == "1")
				{
					rwrd := rwrd * 6;
					bp := bp + 3;
				}
				else if(sl.[3] == "2")
				{
					rwrd := 5 * sx;
				}
				else if(sl.[3] == "3")
				{
					rwrd := 500 * sc;
				}
				if(sl.[2] == pc.name)
				{
					rwrd := rwrd * 2;
					bp := bp * 2;
				}
				if(i < 10)
				{
					rwrd := rwrd;
					bp := bp;
				}
				else if(i < 20)
				{
					rwrd := rwrd * 2;
					bp := bp * 2;
				}
				else if(i < 50)
				{
					rwrd := rwrd * 3;
					bp := bp * 3;
				}
				else if(i < 100)
				{
					rwrd := rwrd * 4;
					bp := bp * 4;
				}
				else if(i > 100)
				{
					rwrd := rwrd * 5;
					bp := bp * 5;
				}
				wait(SFB_TICK,command(CMD_AUTO_TICK));
				exec("say Your spree was a success.",self);
				exec("grin",self);
				wait(SFB_TICK,command(CMD_AUTO_TICK));
				exec("say Your streak is now at "+itoa(i)+". Here is the commission.",self);
				
				act("$2n hands you some coins.",A_SOMEONE, pc, self, null, TO_CHAR);
				i := transfermoney(null,pc,(rwrd/2));	
				experience(rwrd, pc);
				act("<br><div class='hint'>Killing Spree Completed!</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
				act("<div class='xpgain'>You recieve "+itoa(rwrd)+" experience.</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
				i := add_bp(bp,pc);
				if(bp == 1)
					act("<div class='xpgain'>You recieve "+itoa(bp)+" bounty point.</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
				else
					act("<div class='xpgain'>You recieve "+itoa(bp)+" bounty points.</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
			}
			else if ("CLOSED" == sl.[1])
			{
				exec("say Blast you "+pc.name+", you've already taken a reward for that spree!",self);
			}
			else
			{
				exec("say Get moving "+pc.name+"! You're not done yet!",self);
			}
		}
		else
			exec("say You'd need to start a killing spree today before you can end one "+pc.name+"!",self);
	}
	else
		exec("say Perhaps you should run a killing spree before you try to end one "+pc.name+"!",self);
	
:donezo:

	unsecure(pc);
	goto start;
}
dilend // END end_spree

dilbegin integer add_bp(n : integer, u : unitptr);
var
	bp : integer;
code
{
	if(not("$bounty_points" in u.extra))
		addextra (u.extra,{"$bounty_points"},"0");	
	bp := atoi(u.extra.["$bounty_points"].descr);
	bp := bp + n;
	if(bp < 0)
		bp := 0;
	u.extra.["$bounty_points"].descr := itoa(bp);
	return(bp);
}
dilend

// n should equal either 2 or 3. Returning date to Month = 2, day = 3
// ex s := stringdate(2); s == 2021.2
// ex s := stringdate(3); s == 2021.2.21
dilbegin string stringdate(n : integer);
var
	sdate   : string;
	sltime	: stringlist;
	slmonth : stringlist;
	i : integer;
code
{
	slmonth := {"Skip","Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec"};
	sltime := split(asctime(realtime), " "); 

	// This is to fix an apparent bug where an extra space in the asctime output can result in
	// an empty string being populated into the stringlist, fucking up the parsing.
	while ("" in sltime)
		substring(sltime,"");
	
	i := 0;
	while(i <= 12)
	{
		if(slmonth.[i] == sltime.[1])
			break;
		i := i + 1;
	}
	if(n == 3)
		sdate := sltime.[4] + "." + itoa(i) + "." + sltime.[2];
	if(n == 2)
		sdate := sltime.[4] + "." + itoa(i);
	
	return(sdate);
}
dilend

// feed a string returned from strdate() to return an integer you can compare.
dilbegin integer dateformat (s : string);
var
	i : integer;
	buf : string;
	slbuf : stringlist;
code
{
	buf := "";
	slbuf := split(s,".");
	i := 0;
	while (i < length(slbuf))
	{
		if (atoi(slbuf.[i]) < 10)
			slbuf.[i] := "0" + slbuf.[i];
		buf := buf + slbuf.[i];
		i := i + 1;
	}
	i := atoi(buf);
	return (i);
}
dilend // END dateformat

dilbegin make_bounty();
external
	integer add_bp@bounty(n : integer, u : unitptr);
	integer dateformat@bounty (s : string);
	string stringdate@bounty (n : integer);
var 
	item 	: unitptr;
	pc 		: unitptr;
	exdp   	: extraptr; 
	i 		: integer;
	targ	: unitptr;
	bty		: unitptr;
	arg 	: string;
	sdate   : string;
	smonth	: string;
	sltime	: stringlist;
	slmonth : stringlist;
	slfmonth : stringlist;
	dspl	: stringlist;
	blocked : stringlist;
	slextra : stringlist;
	estreak : extraptr;
	which   : string;
	idate	: integer;
	ibdate	: integer;
	sltemp  : stringlist;
	buf 	: string;
code
{
:init:

	slmonth := {"Skip","Jan","Feb","Mar","Apr","May","Jun","Jul","Aug","Sep","Oct","Nov","Dec"};
	slfmonth := {"Skip","January","February","March","April","May","June","July","August","September","October","November","December"};
	blocked := {"basis","death","spells","skills","commands","baseget","basemove","wiz","baselook",
				"update","help","boards","comm","socials","set","mail","function","restrict","newbie",
				"justice","arena","competition","clan","holiday20","guild_quest","guild_paths",
				"assassin","clericguild","fightersguild","mageguild","necro_guild","offices","paladin_guild",
				"sorcerer","thievesguild","druid","knight","ranger","rebirth","barbarian","bounty"};

:start:

	heartbeat := PULSE_SEC;
	wait(SFB_CMD, (activator.type == UNIT_ST_PC) and (command("say")) and 
		(("contract" == argument) or ("daily contract" == argument) or ("monthly contract" == argument) or ("bonus contract" == argument)));
	
	arg := argument;
	pc := activator;
	secure(pc, start);
	
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	buf := "";
	
	if(pc.level < 50)
	{
		exec("Say You need to be at least level 50 to take contracts "+pc.name+"!", self);	
		goto start;
	}

	slextra := {""};
	sltime := split(asctime(realtime), " "); 

	// This is to fix an apparent bug where an extra space in the asctime output can result in
	// an empty string being populated into the stringlist, fucking up the parsing.
	while ("" in sltime)
		substring(sltime,"");

	i := 0;
	while(i <= 12)
	{
		if(slmonth.[i] == sltime.[1])
			break;
		i := i + 1;
	}
	sdate := stringdate(3);
	smonth := stringdate(2);
	
	if(arg == "daily contract")
	{
		arg := "daily";
		goto this_bounty;
	}
	if(arg == "monthly contract")
	{
		arg := "monthly";
		goto this_bounty;
	}
	if(arg == "bonus contract")
	{
		arg := "bonus";
		goto this_bounty;
	}
	
	exec("Say So "+pc.name+" you'd like to see a contract.", self);	
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	
:which_bounty:
	
	exec("Say Would you like the daily, monthly, or a bonus contract?", self);	
	exec("Say You may also shake your head to decline.", self);
	wait(SFB_CMD, (activator.type == UNIT_ST_PC) and ((command("say")) and (("daily" in argument) or ("monthly" in argument) or ("bonus" in argument)))  or (command("shake")));
	
	if (command ("shake"))
		goto decline;
	
	arg := getword(argument);

:this_bounty:
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	if (arg == "daily")
	{
		exdp := pc.extra.["$daily_target"];
		slextra.[0] := "$daily_target";
		which := arg;
	}
	else if (arg == "monthly")
	{
		sdate := smonth;
		exdp := pc.extra.["$monthly_target"];
		slextra.[0] := "$monthly_target";
		which := arg;
	}
	else if (arg == "bonus")
	{
		sdate := "bonus";
		exdp := pc.extra.["$bonus_target"];
		slextra.[0] := "$bonus_target";
		which := arg;
	}
	else
		goto which_bounty;
	
:give_bounty:

	if(exdp != null)
	{
		dspl := split(exdp.descr, "|");
		ibdate := dateformat(dspl.[0]);
		idate := dateformat(sdate);
		if (dspl.[0] == sdate)
		{
			if((sdate == "bonus") and ("DONE" in dspl))
			{
				while ("$bonus_target" in pc.extra)
					subextra(pc.extra, "$bonus_target");
				goto new_target;
			}
			else if(sdate == "bonus")
				goto skip_bonus;
			goto have_bounty;
		}
		
		if((not("DONE" in dspl)) or (idate > ibdate+2))	
		{
			estreak := pc.extra.["$"+which+"_streak"];
			if (estreak != null)
			{
				if (atoi(estreak.descr) > 1)
					exec("say Ha! It seems your streak has come to an end. You had closed "+estreak.descr+" "+which+" bounties in a row!",self);
				else
					exec("say Well, you weren't able to close that last "+which+" contract.",self);
				subextra(pc.extra, "$"+which+"_streak");
			}
		}
		subextra(pc.extra, "$"+which+"_target");
	}
:new_target:
	targ := findrndunit(pc,FIND_UNIT_WORLD,UNIT_ST_NPC);
	if (targ.zoneidx in blocked)
		goto new_target;
	if (((targ.race > RACE_CNIDARIA_MIN) and (targ.race <= RACE_CNIDARIA_MAX)) or
       ((targ.race > RACE_PLANT_MIN) and (targ.race <= RACE_PLANT_MAX)))
		goto new_target;
	addextra (pc.extra,slextra,sdate + "|" + targ.symname + "|" + targ.title);
	if(which == "monthly")
		which := slfmonth.[i];
	
	buf := "<br><div class='cpw'>Contract Date: </div><div class='cr'>" + sdate +
		"<br><div class='cpw'>Contract Term: </div><div class='cr'>" + which +
		"<br><div class='cpw'>Target Name: </div><div class='cr'>" + targ.name + 
		"<br><div class='cpw'>Target Alias: </div><div class='cr'>" + targ.title + 
		"<br><div class='cpw'>Target Description: </div><div class='cr'> " + targ.outside_descr;
	if(not(findsymbolic(pc, "tablet@bounty", FIND_UNIT_INVEN)))
	{
		exec("Say Here you go "+pc.name+", a "+which+" contract.", self);	
		bty := load("contract@bounty");
		bty.extra.[0].descr := bty.extra.[0].descr + buf;
		addextra (bty.extra,{"$symname"},targ.symname);
		addextra (bty.extra,{"$player"},pc.name);
		addextra (bty.extra,{"$date"},sdate);
		link(bty,pc);
		act ("$2n quietly slips you a contract.", A_SOMEONE, pc, self, null, TO_CHAR);
		act ("$2n quietly slips $1n something.", A_SOMEONE, pc, self, null, TO_REST);
	}
	else
		exec("say I've updated your tablet with the new "+which+" contract, "+pc.name,self);
	
	act (buf, A_ALWAYS, pc, null, null, TO_ALL);	
	goto the_end;
	
:have_bounty:
	// maybe we should allow multiples?
	// check to see if the first is done. 
	// how do we allow cancellation if you cant complete?
	exec("say Bugger off! You've already taken a "+which+" contract.",self);
	goto the_end;
	
:skip_bonus:
	
	exec("say You've already taken a "+which+" contract.",self);
	exec("say Are you suggesting that you'd like to abandon this one, and get another?",self);
	wait(SFB_CMD, (activator.type == UNIT_ST_PC) and (command("nod") or command("shake")));
	
	pc := activator;
	
	if (command ("shake"))
		goto decline;
	
	if(command ("nod"))
	{
		heartbeat := PULSE_SEC/4;
		wait(SFB_TICK,command(CMD_AUTO_TICK));
		exec("cackle",self);
		experience(-25000,pc);
		subextra(pc.extra, "$bonus_target");
		subextra(pc.extra, "$bonus_streak");
		act("<div class='xpgain'>You have lost 25000 experience points!</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
		
		i := add_bp(0,pc);
		if(i >= 5)
			act("<div class='xpgain'>You have lost 5 bounty points!</div>",A_ALWAYS, pc, null, null, TO_CHAR);
		else
			act("<div class='xpgain'>You have lost "+itoa(i)+" bounty points!</div>",A_ALWAYS, pc, null, null, TO_CHAR);
		i := add_bp(-5,pc);
		goto the_end;
	}
	
:decline:
	heartbeat := PULSE_SEC/4;
	wait(SFB_TICK,command(CMD_AUTO_TICK));
	exec("Say Very well then.", self);
:the_end:
	unsecure(pc);
	goto start;

}
dilend

dilbegin claim_bounty();
external
	string stringdate@bounty(n : integer);
	integer add_bp@bounty(n : integer, u : unitptr);
var 
	pc 		: unitptr;
	med 	: unitptr;
	edaily 	: extraptr;
	emonth  : extraptr;
	ebonus  : extraptr;
	estreak : extraptr;
	etotal 	: extraptr;
	emax    : extraptr;
	smtarg	: string;
	sdtarg	: string;
	sbtarg  : string;
	sdone	: string;
	sdate   : string;
	etarg 	: extraptr;
	sldt	: stringlist;
	slmt	: stringlist;
	slbt	: stringlist;
	slstr   : stringlist;
	sltot   : stringlist;
	slnew	: stringlist;
	i   	: integer;
	targ    : unitptr;
	rwrd    : integer;
	arg 	: string;
	bp		: integer;
code
{

:start:

	on_activation((self.position == POSITION_FIGHTING), skip);
	wait(SFB_DONE, command("give") and (target == self));
	
	pc := activator;
	med := medium;
	secure(pc, labsecure);
	
	slnew := {""};
	substring (slnew,"");
	if ((self.inside.symname == "mulligan@bounty") and (self.inside.symname == med.symname))
	{
		exec("snicker",self);
		exec("say So you need to be freed from your contract "+pc.name+"?",self);
		exec("say It seems your... documentation is in order. Just say 'daily' or 'monthly' and it will be ended.",self);
		wait(SFB_CMD, (activator.type == UNIT_ST_PC) and ((command("say")) and (("daily" == argument) or ("monthly" == argument))));
	
		arg := argument;
		wait(SFB_TICK,command(CMD_AUTO_TICK));
		if (arg == "daily")
		{			
			if("DONE" in pc.extra.["$daily_target"].descr)
			{
				exec("say Your daily contract is already closed "+pc.name+"!",self);
				exec("give mulligan "+pc.name, self);
				goto start;
			}
			subextra(pc.extra, "$daily_target");
		}
		else if (arg == "monthly")
		{
			if("DONE" in pc.extra.["$monthly_target"].descr)
			{
				exec("say Your monthly contract is already closed "+pc.name+"!",self);
				exec("give mulligan "+pc.name, self);
				goto start;
			}
			subextra(pc.extra, "$monthly_target");
		}
		
		destroy(self.inside);
		exec("say Consider the contract forgotten "+pc.name+".",self);
		goto start;
	}
	
	if((self.inside.symname != "head@death") and (self.inside.symname == med.symname))
	{
		exec("say What would I do with this, "+pc.name+"?",self);
		exec("give "+self.inside.name+" "+pc.name,self);
		goto start;
	}
	if(dilfind("spree_m@bounty",pc))
	{
		exec("say Try again after your spree, "+pc.name+"!",self);
		exec("give "+self.inside.name+" "+pc.name,self);
		goto start;
	}
	
	edaily := pc.extra.["$daily_target"];
	emonth := pc.extra.["$monthly_target"];
	ebonus := pc.extra.["$bonus_target"];
	sldt := split(edaily.descr,"|");
	slmt := split(emonth.descr,"|");
	slbt := split(ebonus.descr,"|");
	sdtarg := sldt.[2];
	smtarg := slmt.[2];
	sbtarg := slbt.[2];

	// if you have turned in a valid bounty
	
	if("The decapitated head of "+sdtarg+" is laying here." == self.inside.outside_descr)
	{
		etarg := edaily;
		emax := pc.extra.["$daily_max"];
		estreak := pc.extra.["$daily_streak"];
		etotal := pc.extra.["$daily_total"];
		sdone := "daily"; 
		sdate := stringdate(3); 
		
		if (estreak == null)
		{
			addextra (pc.extra,{"$daily_streak"},"0");
			estreak := pc.extra.["$daily_streak"];
		}
		if (etotal == null)
		{
			addextra (pc.extra,{"$daily_total"},"0");
			etotal := pc.extra.["$daily_total"];
		}
		if (emax == null)
		{
			addextra (pc.extra,{"$daily_max"},"0");
			emax := pc.extra.["$daily_max"];
		}
	}
	else if ("The decapitated head of "+smtarg+" is laying here." == self.inside.outside_descr)
	{
		etarg := emonth;
		emax := pc.extra.["$monthly_max"];
		estreak := pc.extra.["$monthly_streak"];
		etotal := pc.extra.["$monthly_total"];
		sdone := "monthly";
		sdate := stringdate(2);

		if (estreak == null)
		{
			addextra (pc.extra,{"$monthly_streak"},"0");
			estreak := pc.extra.["$monthly_streak"];
		}
		if (etotal == null)
		{
			addextra (pc.extra,{"$monthly_total"},"0");
			etotal := pc.extra.["$monthly_total"];
		}
		if (emax == null)
		{
			addextra (pc.extra,{"$monthly_max"},"0");
			emax := pc.extra.["$monthly_max"];
		}
	}
	else if ("The decapitated head of "+sbtarg+" is laying here." == self.inside.outside_descr)
	{
		etarg := ebonus;
		emax := pc.extra.["$bonus_max"];
		estreak := pc.extra.["$bonus_streak"];
		etotal := pc.extra.["$bonus_total"];
		sdone := "bonus";
		sdate := stringdate(3);

		if (estreak == null)
		{
			addextra (pc.extra,{"$bonus_streak"},"0");
			estreak := pc.extra.["$bonus_streak"];
		}
		if (etotal == null)
		{
			addextra (pc.extra,{"$bonus_total"},"0");
			etotal := pc.extra.["$bonus_total"];
		}
		if (emax == null)
		{
			addextra (pc.extra,{"$bonus_max"},"0");
			emax := pc.extra.["$bonus_max"];
		}
	}
	else
	{
		exec("cackle",self);
		exec("say Kill happy arent we "+pc.name+"? This wasn't your target.",self);
		exec("give "+self.inside.name+" "+pc.name,self);
		goto labsecure;
	}
	
	
	slstr := split(etarg.descr, "|");
	if("DONE" in slstr)
		goto already_done;
	if(not("bonus" in slstr))
		if (slstr.[0] != sdate)
			goto already_done;
		
	destroy(self.inside);	
	
	// increment pointers as necessary
	
	estreak.descr := itoa(atoi(estreak.descr) + 1);
	etotal.descr := itoa(atoi(etotal.descr) + 1);
	etarg.descr := etarg.descr + "|DONE";
	if((atoi(estreak.descr) > atoi(emax.descr)) and (sdone != "bonus"))
		emax.descr := estreak.descr;
	
	
	
	/*

This code is for creating the signs and tracking leaders. 
It is all based on extras on the PC's, so we can capture
data for now until we're ready to implement.

I think we should probably ditch this system entirely, and
leverage the competition boards. Will investigate.

	targ := findsymbolic("day_sign@bounty");
	if (targ.extra.[0].descr == "")
	{
		sendtext("freshies - add "+pc.name,pc);
		addstring(slnew, pc.name);
		addstring(slnew, estreak.descr);
		goto freshies;
	}
	slstr := split(replace("<br>"," - ",targ.extra.[0].descr)," - ");
	substring (slstr,"");
	i := 0;
	while(i < length(slstr))
	{
		sendtext("i == "+itoa(i)+" lenght == "+ itoa(length(slstr)),pc);
		if(atoi(estreak.descr) > atoi(slstr.[i+1]))
		{
			sendtext("greater - add "+pc.name,pc);
			addstring(slnew, pc.name);
			addstring(slnew, estreak.descr);
			if (pc.name != slstr.[i])
			{
				sendtext("great and not equal - add "+slstr.[i],pc);
				addstring(slnew, slstr.[i]);
				addstring(slnew, slstr.[i+1]);
			}
		}		
		else if(atoi(estreak.descr) <= atoi(slstr.[i+1]))
		{
			sendtext("less or equal - add "+slstr.[i],pc);
			addstring(slnew, slstr.[i]);
			addstring(slnew, slstr.[i+1]);
			
			// just added this - testing
			// trying to handle scenarios where the board isnt full and if
			// have less than someone else
			//addstring(slnew, pc.name);
			//addstring(slnew, estreak.descr);
		}
		if (i == 18)
			break;
		i := i + 2;
	}
:freshies:
	i := 0;
	targ.extra.[0].descr := "";
	sendtext("i == "+itoa(i)+" length == "+ itoa(length(slnew)),pc);
	while (i <= length(slnew)-2)
	{
		sendtext("i == "+itoa(i)+" length == "+ itoa(length(slnew)),pc);
		sendtext("writing slnew: "+slnew.[i],pc);
		targ.extra.[0].descr := targ.extra.[0].descr + slnew.[i];
		targ.extra.[0].descr := targ.extra.[0].descr + " - ";
		targ.extra.[0].descr := targ.extra.[0].descr + slnew.[i+1];
		targ.extra.[0].descr := targ.extra.[0].descr + "<br>";
		i := i + 2;
	}
	
	*/
	
	
	destroy(self.inside);
	exec("say Yes, "+slstr.[2]+"'s head.",self);
	exec("say It appears we can consider your "+sdone+" contract closed, "+pc.name+".",self);
	exec("grin",self);
	
	// The reward section //
	
	exec("say Your streak is now at "+estreak.descr+". Here is the commission.",self);
	
	// set streak length
	i := atoi(estreak.descr);
	if(sdone == "daily")
	{
		// base value in XP.
		// cash reward will be iron = xp value / 2
		rwrd := 1000;
		bp := 2;
		if (i < 7)
			rwrd := (rwrd * i) + 5000;
		else if (i < 10)
			rwrd := 15000;
		else if (i < 100)
		{
			bp := bp * 2;
			rwrd := rwrd * (i * 3);
		}
		else if (i < 200)
		{
			bp := bp * 3;
			rwrd := rwrd * (i * 4);
		}
		else if (i < 300)
		{
			bp := bp * 4;
			rwrd := rwrd * (i * 5);
		}
		else if (i >= 300)
		{
			bp := bp * 5;
			rwrd := rwrd * (i * 6);
		}
		
		
		// streak milestone rewards
		if(i == 14 or i == 30 or i == 60 or i == 90 or i == 180 or i == 365)
		{
			exec("say I've included a little something for making "+itoa(i)+" daily contracts in a row.",self);
			bp := bp * 2;
			rwrd := rwrd * 2;
		}
		
	}
	else if (sdone == "monthly")
	{
		// base value in XP.
		// cash reward will be iron = xp value / 2
		rwrd := 10000;
		bp := 10;
		if (i < 3)
		{
			rwrd := rwrd * (i * 2);
		}
		else if (i < 10)
		{	
			bp := bp * 2;
			rwrd := rwrd * (i * 3);
		}
		else if (i < 15)
		{
			bp := bp * 3;
			rwrd := rwrd * (i * 4);
		}
		else if (i >= 20)
		{
			bp := bp * 4;
			rwrd := rwrd * (i * 5);
		}
		
		// streak milestone rewards
		if(i == 6 or i == 12 or i == 18 or i == 24)
		{
			exec("say I've included a little something for making "+itoa(i)+" monthly contracts in a row.",self);
			bp := bp * 2;
			rwrd := rwrd * 2;
		}
	}
	else if (sdone == "bonus")
	{
		// base value in XP.
		// cash reward will be iron = xp value / 2
		rwrd := 5000;	
		bp := 1;
		// streak milestone rewards
		if((i == 10) or (i == 20) or (i == 30) or (i == 40) or (i == 50))
		{
			exec("say I've included a little something for making "+itoa(i)+" bonus contracts in a row.",self);
			bp := 2;
			rwrd := rwrd * 2;
		}
	}
	
	act("<br>$2n hands you some coins.",A_SOMEONE, pc, self, null, TO_CHAR);
	i := transfermoney(null,pc,(rwrd/2));	
	experience(rwrd, pc);
	act("<br><div class='hint'>Contract Completed!</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
	act("<div class='xpgain'>You recieve "+itoa(rwrd)+" experience.</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
	i := add_bp(bp,pc);
	if(bp == 1)
		act("<div class='xpgain'>You recieve "+itoa(bp)+" bounty point.</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
	else
		act("<div class='xpgain'>You recieve "+itoa(bp)+" bounty points.</div>",A_ALWAYS, pc, null, null, TO_CHAR); 
	goto labsecure;
	// End reward section // 
	
:already_done:

	if (atoi(estreak.descr) > 1)
		exec("say This contract has already closed! It seems your streak of "+estreak.descr+" "+sdone+" contracts has come to an end.",self);
	else
		exec("say Your first contract in the streak, and it had already closed!",self);
	subextra(pc.extra, "$"+sdone+"_streak");
	
:labsecure:

	unsecure(pc);	
	goto start;
}
dilend		

dilbegin restore_pot(aff : string, strength : string);
var
	pc : unitptr;
code
{
	wait(SFB_CMD, command("quaff") and ("potion" in argument));
	pc := activator;

	if(aff == "restore")
	{
		if(strength == "full")
		{
			pc.hp := pc.max_hp;
			pc.endurance := pc.max_endurance;
			pc.mana := pc.max_mana;
		}
		else if(strength == "strong")
		{
			if(pc.hp < ((pc.max_hp / 4)*3))
				pc.hp := ((pc.max_hp / 4)*3);
			if(pc.endurance < ((pc.max_endurance / 4)*3))
				pc.endurance := ((pc.max_endurance / 4)*3);
			if(pc.mana < ((pc.max_mana / 4)*3))
				pc.mana := ((pc.max_mana / 4)*3);
		}
		else if(strength == "half")
		{
			if(pc.hp < (pc.max_hp / 2))
				pc.hp := (pc.max_hp / 2);
			if(pc.endurance < (pc.max_endurance / 2))
				pc.endurance := (pc.max_endurance / 2);
			if(pc.mana < (pc.max_mana / 2))
				pc.mana := (pc.max_mana / 2);
		}
	}
	else if (aff == "hp")
	{
		if(strength == "full")
			pc.hp := pc.max_hp;
		else if(strength == "strong")
		{
			if(pc.hp < ((pc.max_hp / 4)*3))
				pc.hp := pc.hp + ((pc.max_hp / 4)*3);
		}
		else if(strength == "half")
		{
			if(pc.hp < (pc.max_hp / 2))
				pc.hp := pc.hp + (pc.max_hp / 2);
		}
	}
	else if (aff == "endurance")
	{
		if(strength == "full")
			pc.endurance := pc.max_endurance;
		else if(strength == "strong")
		{
			if(pc.endurance < ((pc.max_endurance / 4)*3))
				pc.endurance := pc.endurance + ((pc.max_endurance / 4)*3);
		}
		else if(strength == "half")
		{
			if(pc.endurance < (pc.max_endurance / 2))
				pc.endurance := pc.endurance + (pc.max_endurance / 2);
		}
	}
	else if (aff == "mana")
	{
		if(strength == "full")
			pc.mana := pc.max_mana;
		else if(strength == "strong")
		{
			if(pc.mana < ((pc.max_mana / 4)*3))
				pc.mana := pc.mana + ((pc.max_mana / 4)*3);
		}
		else if(strength == "half")
		{
			if(pc.mana < (pc.max_mana / 2))
				pc.mana := pc.mana + (pc.max_mana / 2);
		}
	}
	quit;
}
dilend

dilbegin hint (which : string, htype : string);
var
	pc : unitptr;
	zn : string;
	exdp : extraptr;
	zp : zoneptr;
	slt : stringlist;
	uhin : unitptr;
	sdate : string;
	targ : unitptr;
code
{
:start:
	wait(SFB_CMD, (command("look")) and ("hint" in argument));
	pc := activator;
	zp := zhead();
	heartbeat := PULSE_SEC;
	if(which == "d")
		exdp :=  pc.extra.["$daily_target"];
	else if(which == "m")
		exdp :=  pc.extra.["$monthly_target"];
	slt := split(exdp.descr,"|");
	if(slt.[3] == "DONE")
	{
		act("You can only use your hint on an open contract!",A_SOMEONE, pc, self, null, TO_CHAR);
		block;
		goto start;
	}
	sdate := slt.[0];
	targ := findsymbolic(slt.[1]);
	zn := targ.zoneidx;
	self.extra.[0].descr := self.extra.[0].descr + 
	"<br><div class='cpw'>Contract Date: </div><div class='cpc'>" + sdate +
	"<br><div class='cpw'>Target Name: </div><div class='cpc'>" + targ.name + 
	"<br><div class='cpw'>Target Alias: </div><div class='cpc'>" + targ.title + 
	"<br><div class='cpw'>Target Description: </div><div class='cpc'> " + targ.outside_descr;
	while (zp)
	{
		if(zp.name == zn)
		{
			slt := split(zp.title,"@");
			if(htype == "z" )
			{
				self.extra.[0].descr := self.extra.[0].descr + 
				"<br><div class='cpw'>Zone Name: </div><div class='cpc'>" + slt.[0];
			}
			else if(htype == "c")
			{
				self.extra.[0].descr := self.extra.[0].descr + 
				"<br><div class='cpw'>Continent Name: </div><div class='cpc'>" + slt.[1];
			}
			wait(SFB_TICK,command(CMD_AUTO_TICK));
			quit;
		}
		zp := zp.next;
	}
	quit;
}
dilend

dilbegin shop(items : stringlist);
external
	integer add_bp@bounty(n : integer, u : unitptr);
var
	pc : unitptr;
	i : integer;
	ln : integer;
	buf : string;
	j : integer;
	bp : string;
	uitem : unitptr;
	ibp : integer;
	arg : string;
code
{
:init:

	heartbeat := PULSE_SEC;
	
	ln:=length(items);


:start:

	buf:="<b><div class='cpn'>Bounty Hunters Guild Shop</div></b><br>";
	buf:=buf+"<table><tr><th>Item # - </th><th>Item Name</th><th> - Bounty Points</th></tr>";
	i := 0;
	j := 1;

	wait(SFB_CMD, (command("list") or command("buy")));
	block;

	if(argument)
		arg := argument;
	
	pc := activator;
	ibp := add_bp(0,pc);
	bp := itoa(ibp);
	
	if(command("list"))
		goto list;
	if(command("buy"))
		goto buy;

:list:


	while(i < (ln-2))
	{
		buf:=buf+"<tr><td>"+itoa(j)+" - </td><td>"+items.[i+1]+"</td><td> - "+items.[i+2]+"</td></tr>";
		i := i+3;
		j := j+1;
	}
	
	bp := "</table><br><div class='cpc'>You have "+bp+" Bounty Points available.</div>";
	sendtext(buf+bp,pc);
	goto start;
	
:buy:
	
	while(i < (ln-2))
	{
		if(atoi(arg) == j)
		{
			if(atoi(items.[i+2]) <= ibp)
			{
				ibp := add_bp((atoi(items.[i+2])* (-1)),pc);
				uitem := load(items.[i]+"@bounty");
				link(uitem, pc);
				exec("say Here is your "+uitem.name+" "+pc.name+"!",self);
			}
			else
				exec("say You don't have "+items.[i+2]+" bounty points "+pc.name+"!",self);
			goto start;
		}
		i := i+3;
		j := j+1;
	}
	exec("say Please select an item number from the list.",self);
	goto start;
	
}
dilend


%rooms

entry_room
title "<div class='cpn'>Entrance to Bounty Hunters Guild</div>"
descr
"Stepping out from bustle of Kings Road, the entrance to the Bounty Hunters 
Guild is a distinct change of environment. The large stone building extends 
high into the air and has a commanding presence. Tall trees provide ample 
shade during the bright days, and plenty of routes to approach... quietly. 
It seems that there has been significant wealth brought to the guild over 
many generations. A large set of double-doors extend into the darkness."

movement SECT_CITY
flags {UNIT_FL_NO_MOB}
ALWAYS_LIGHT

north to bounty_1;
south to kingroad5@ratswarf;

end

bounty_1
title "<div class='cpn'>Inside the Guild</div>"
descr
"Through the large double-doors the quiet of this space encompasses you. While 
the building is largely unadorned and simple rough-hewn stone. The sheer size 
and scale of the place speak to the significant investments that have been 
made over many years. To the east and west more rooms expand into the vast hall, 
and to the south the outdoors and access to Kings Road can be seen in the distance."
flags {UNIT_FL_INDOORS}
movement SECT_CITY
ALWAYS_LIGHT

south to entry_room;
west to contracts;
east to bounty_h;
north to shrine;

end

bounty_h
title "<div class='cpn'>A Hallway in the Guild</div>"
descr
"Here off the grand entry, the air is still and quiet. Despite this respite 
from the bustle of the world outside, there is a clear sense of life here. 
There is a purpose to every stone, and every doorway. The floors are well 
worn, and the hallway has clearly seen significant traffic over the years. 
There are doorways to the north, south, and east. To the west, the grand 
entry and the large double-doors leading out can be seen."
flags {UNIT_FL_INDOORS}
movement SECT_CITY
ALWAYS_LIGHT

west to bounty_1;
east to shop;
north to smithy;
south to jewels;

end

shop
title "<div class='cpn'>The Guild Shop</div>"
descr
"The many wares which are made available to guild members can be seen adorning 
nearly every wall and visible surface. From potions and tinctures, to notes and 
papers, and even jewely and weapons, there is an impressive array available. 
There is a large counter which has been cleared to make way for transaction"
flags {UNIT_FL_INDOORS}
movement SECT_CITY
ALWAYS_LIGHT

west to bounty_h;

end

smithy
title "<div class='cpn'>The Smithy</div>"
descr
"Through the doorway a large working Smithy is unveiled. The design of the room 
allows for air to flow freely in to feed the great bellows that keep the furnace 
and smelter hot. Large piles of ore are lain out, and amongst them chests of finer 
metals and precious stones. The Smithy is alive with work as the hands that support 
The Blacksmith work their art. This appears to be a formidable smithy capable of 
supplying far greater volumes of arms and armor than it seems like the Hunters 
would need."
flags {UNIT_FL_INDOORS}
movement SECT_CITY
ALWAYS_LIGHT

south to bounty_h;

dilcopy anvils@bounty();

end

jewels
names {"packages"}
title "<div class='cpn'>The Jeweler's Workshop</div>"
descr
"South of the hallway the Jeweler's Workshop is a quiet juxtaposition to the 
ringing of hammers on anvils that periodically rings out from the Smithy to 
the north. Chests of fine metals and precious gemstones are arrayed strategically 
throughout the space. A small-batch smelter appropriate to the task burns in the 
corner, and a range of benches covered in files and other tools of the trade 
fill the space. It is clear that the Bounty Hunters produce high quality pieces."
flags {UNIT_FL_INDOORS}
movement SECT_CITY
ALWAYS_LIGHT

north to bounty_h;

end

contracts
names {"contracts"}
title "<div class='cpn'>The Contracts Room</div>"
descr
"Here away from the main hall of the guild, a quiet room filled with maps 
is in front of you. On every wall and table a map of a different part of 
the world sits. A large desk can be seen in the middle of the room, 
providing ample space for managing the myriad contracts the guild processes. 
Behind the desk darkness overcomes what light the torches shed."
flags {UNIT_FL_INDOORS, UNIT_FL_SACRED}
movement SECT_CITY
ALWAYS_LIGHT

east to bounty_1;

end

shrine
title "<div class='cpn'>The Bounty Hunters Shrine</div>"
descr
"In the back of the great hall, a peaceful shrine pays homage to the gods 
of the hunt. Candles burn at the base of shrine, sending their flickering 
light into the darkness. Incense burns alongside the candles, and the 
scent brings visions of both frenzied chase, and gratifying calm."
flags {UNIT_FL_INDOORS, UNIT_FL_SACRED}
movement SECT_CITY
ALWAYS_LIGHT

south to bounty_1;

dilcopy pray@bounty();

end

sanctum
title "<div class='cpn'>The Inner Sanctum</div>"
descr
"This is the Inner Sanctum of the Bounty Hunters Guild. Rumors about its existance 
have spread throughout the land, but to most, its assumed that rumor is all it ever 
was. The reality of being here, however, puts those rumors to rest. Like the rest 
of the guild, it is a large, and masterfully wrought stone room. No lack of 
attention was given to the construction, and a well-made yet simple shrine rests 
against the southern wall. The space is not entirely free from decor, as frescos 
have been painted in sections on the walls representing the hunt in a seemingly 
strange and unknown part of history."
flags {UNIT_FL_INDOORS, UNIT_FL_SACRED, UNIT_FL_NO_TELEPORT}
movement SECT_CITY
ALWAYS_LIGHT

dilcopy pray2@bounty();

north to sanctum_1;
east to sanc_h;

end

sanctum_1
title "<div class='cpn'>The Inner Sanctum</div>"
descr
"Here the Bounty Hunters Guild Inner Sanctum expands, and comes to an apparent 
end. The frescos continue across the walls here, and an overall sense of well being 
and peace is here. Despite this, it is with no small sense of apprehension your eyes 
come to rest on simple though plentiful feast laid out across sturdy tables. Food 
an drink enough to feed many is arrayed across the tables, and while of fine quality 
it is simple, and not out of character for what you have noticed of this strange 
place. The room appears empty, and the only exit is back to the south."

extra {"tables", "table"}
"The tables are crafted from thick strong timbers, and could support a small army. 
That, or a very large feast."

extra {"feast"}
"It is somewhat strange to find a feast of fresh hot food laid out here in this 
seemingly lonely place. Dare you partake in this feast?"

flags {UNIT_FL_INDOORS, UNIT_FL_SACRED, UNIT_FL_NO_TELEPORT}
movement SECT_CITY
ALWAYS_LIGHT

dilcopy feast@bounty();

south to sanctum;

end

end

sanc_h
title "<div class='cpn'>A Hallway off the Inner Sanctum</div>"
descr
"A hallway leads away from the main rooms of the Inner Sanctum. The air is 
still and quiet. Despite this respite from the bustle of the world outside, 
there is a clear sense of life here. As with the hall above, there is a 
clear purpose to every stone. The space seems significantly less well 
worn than the outer guild above, and the hallway has seen little traffic 
over the years. There is a doorway east that opens into a vast library. To the 
west, the Inner Sanctum can be seen."
flags {UNIT_FL_INDOORS}
movement SECT_CITY
ALWAYS_LIGHT

west to sanctum;
east to library;

end

library
title "<div class='cpn'>A Hallway off the Inner Sanctum</div>"
descr
"A hallway leads away from the main rooms of the Inner Sanctum. The air is 
still and quiet. Despite this respite from the bustle of the world outside, 
there is a clear sense of life here. As with the hall above, there is a 
clear purpose to every stone. The space seems significantly less well 
worn than the outer guild above, and the hallway has seen little traffic 
over the years. There is a doorway east that opens into a vast library. To the 
west, the Inner Sanctum can be seen."
flags {UNIT_FL_INDOORS}
movement SECT_CITY
ALWAYS_LIGHT

west to sanc_h;

end

%mobiles

hunter
names {"bounty hunter","hunter","man"}    
title "the bounty hunter"        
descr "A tall man with a dark smirk stands in the shadows."
extra{}
"Tall and well muscled, he is of unknown origin. An dark smirk seems to permanently rest on his face. This 
appears to be the man who may offer a contract if you but ask." 

alignment -1000
M_AVG_HUMAN(100,SEX_MALE)
position POSITION_STANDING 

dilcopy make_bounty@bounty();
dilcopy claim_bounty();
dilcopy join();
dilcopy status();

end 

shopkeep
names {"shopkeeper","dwarf"}    
title "the guild shopkeeper"        
descr "A girthy dwarf with a warm smile stands behind the counter."
extra{}
"Girthy, and apparently not unfamiliar with his wine, the shopkeeper nevertheless 
seems to be rather friendly. Simply ask for his list to select from the guild wares." 

alignment 0
M_AVG_DWARF(100,SEX_MALE)
position POSITION_STANDING 

dilcopy shop@bounty({"tablet","Bounty Hunters Tablet","150",
						"full_restore","Potion of Full Restoration","60",
						"strong_restore","Potent Potion of Restoration","30",
						"half_restore","Potion of Restoration","15",
						"full_mana","Potion of Full Mana","20",
						"strong_mana","Potent Potion of Mana","10",
						"half_mana","Potion of Mana","5",
						"full_end","Potion of Full Endurance","20",
						"strong_end","Potent Potion of Endurance","10",
						"half_end","Potion of Endurance","5",
						"full_heal","Potion of Full Healing","20",
						"strong_heal","Potent Potion of Healing","10",
						"half_heal","Potion of Healing","5",
						"mulligan","Contract Breaking Note","80",
						"dz_hint","Daily Hint - Zone","40",
						"dc_hint","Daily Hint - Continent","20",
						"mz_hint","Monthly Hint - Zone","40",
						"mc_hint","Monthly Hint - Continent","20"});

end 

guide
names {"hunters guide","guide","woman"}    
title "the hunters guide"        
descr "A dark-cloaked woman is here, in the shadows."
extra{}
"Not much can be seen of the woman under the dark cloak. A strength can be felt 
as you gaze upon her. It would be wise to choose your words carefully." 

alignment -1000
M_AVG_ELF(100,SEX_FEMALE)
position POSITION_STANDING 

dilcopy expel();
dilcopy guide();

end 

historian
names {"the historian","historian","centaur"}    
title "The Historian"        
descr "An aged centaur stands here with a look of knowing."
extra{}
"Tall and powerful despite clearly showing the signs of significant age, 
The Historian is a somewhat mysterious creature. Not much is known 
about him, except that he has served the Hunters Guild longer than any
other living beings." 

alignment 0
M_UNICORN_XLARGE(100,SEX_MALE)
position POSITION_STANDING 

end 

blacksmith
names {"the blacksmith","blacksmith","groll"}    
title "The Blacksmith"        
descr "A strong groll blacksmith stands at the forge."
extra{}
"The Blacksmith is a central figure in the Bounty Hunters guild. 
With every swing of his hammer, he brings glory and strength to the 
members. He works to craft the unique weapons, and armors which 
serve the Hunters. Ask about the opportunity to leverage his weapons 
on a spree." 

alignment 0
M_UNICORN_XLARGE(100,SEX_MALE)
position POSITION_STANDING 

dilcopy new_spree@bounty();
dilcopy end_spree@bounty();

end 

jeweler
names {"the jeweler","jeweler","half-elf"}    
title "The Jeweler"        
descr "A half-elven jeweler is here working her craft."
extra{}
"The Bounty Hunters have relied on the finely-wrought jewelery 
produced by The Jeweler to both reward their members, and support 
the payments which work to lubricate hands and minds in favor 
of their work." 

alignment 0
M_AVG_HALF_ELF(100,SEX_FEMALE)
position POSITION_STANDING 
exp 400

dilcopy delivery@bounty();

end 

%objects

contract
names {"contract"}
title "a contract"
descr "A crisp paper is here."
extra {}
"<div class='cr'>REWARD!!</div><br>"
manipulate {MANIPULATE_TAKE}
weight 1
cost 1 IRON_PIECE
end

tablet
names {"bounty hunters tablet","btyhntrtblt","tablet"}
title "the bounty hunters tablet"
descr "A strange stone tablet lies here."
extra {}
""
manipulate {MANIPULATE_TAKE}
weight 1
cost 1 IRON_PIECE

dilcopy tablet@bounty();

end

// longest streaks
assn_plaque
names {"top assassin plaque","top assassin","plaque","1"}
title "the top assassins plaque"
descr "A <a cmd='read #'>top assassin plaque</a> is here."
weight 1
cost 1 IRON_PIECE
extra {}
""
end
//  longest active monthly streak
mnth_sign
names {"monthly assassin sign","monthly assassin","sign","2"}
title "the monthly assassins sign"
descr "An active <a cmd='read #'>monthly assassin sign</a> is here."
weight 1
cost 1 IRON_PIECE
extra {}
""
end
// longest active daily streak
day_sign
names {"daily assassin sign","daily assassin","sign","3"}
title "the daily assassins sign"
descr "An active <a cmd='read #'>daily assassin sign</a> is here."
weight 1
cost 1 IRON_PIECE
extra {}
""
end

//most total dailies
tday_board
names {"total daily assassin board","total assassin","board","4"}
title "the total daily assassins board"
descr "A <a cmd='read #'>total assassin board</a> is here."
weight 1
cost 1 IRON_PIECE
extra {}
""
end
// most total monthlies
tmonth_board
names {"total monthly assassin board","total monthly","board"}
title "the total monthly assassins board"
descr "A <a cmd='read #'>total monthly board</a> is here."
weight 1
cost 1 IRON_PIECE
extra {}
""
end

bty_sign
names {"bounty sign","sign","instructions"}
title "some instructions"
descr "Some helpful <a cmd='read #'>instructions</a> are here."
extra {}
"Say 'contract' to hear the contract options available.<br><br>
Say 'contract status' to hear the status of any existing contracts, 
and for the option to receive a new contract card."
weight 1
cost 1 IRON_PIECE
extra {}
""
end

spree_sign
names {"spree sign","sign","instructions"}
title "some instructions"
descr "Some helpful <a cmd='read #'>instructions</a> are here."
extra {}
"Say 'spree' to kick off a new killing spree. You would be well <br>
advised to ensure you have a group with you ready for killing! <br><br>
Say 'end spree' once the time has run out and you are ready to <br>
collect your rewards!<br><br>
You must have completed 20 total bounties of any kind from the <br>
Bounty Hunter in order to lead a spree. Anyone may participate in <br>
following a leader on their spree."

weight 1
cost 1 IRON_PIECE
extra {}
""
end

pkg_sign
names {"package sign","sign","instructions"}
title "some instructions"
descr "Some helpful <a cmd='read #'>instructions</a> are here."
extra {}
"Say 'i need work' to kick off a new package delivery for the jeweler!<br>
Be careful, you dont want to open these packages!"

weight 1
cost 1 IRON_PIECE
extra {}
""
end


//////////////////////////////////////////////////////////////////
//          special rewards and items for bounties.             //
//////////////////////////////////////////////////////////////////

full_restore
names {"potion of full restoration","full restoration","potion"}
title "a potion of full restoration"
descr "A potion of full restoration is here."
extra {}
"A clear glass vial contains a glowing multi-colored substance. It 
appears to be a highly potent potion of restoration."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("restore","full");

end

strong_restore
names {"potent potion of restoration","potent restoration","potion"}
title "a potent potion of restoration"
descr "A potent potion of restoration is here."
extra {}
"A clear glass vial contains a glowing multi-colored substance. It 
appears to be a potent potion of restoration."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("restore","strong");

end

half_restore
names {"potion of restoration","restoration","potion"}
title "a potion of restoration"
descr "A potion of restoration is here."
extra {}
"A clear glass vial contains a glowing multi-colored substance. It 
appears to be a potion of restoration."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("restore","half");

end

full_heal
names {"potion of full healing","full healing","potion"}
title "a potion of full healing"
descr "A potion of full healing is here."
extra {}
"A clear glass vial contains a glowing red substance. It appears 
to be a highly potent potion of healing."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("hp","full");

end

strong_heal
names {"potent potion of healing","potent healing","potion"}
title "a potent potion of healing"
descr "A potent potion of healing is here."
extra {}
"A clear glass vial contains a glowing red substance. It appears 
to be a potent potion of healing."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("hp","strong");

end

half_heal
names {"potion of healing","healing","potion"}
title "a potion of healing"
descr "A potion of healing is here."
extra {}
"A clear glass vial contains a glowing red substance. It appears 
to be a potion of healing."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("hp","half");

end

full_end
names {"potion of full endurance","full endurance","potion"}
title "a potion of full endurance"
descr "A potion of full endurance is here."
extra {}
"A clear glass vial contains a glowing green substance. It appears 
to be a highly potent potion of endurance."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("endurance","full");

end

strong_end
names {"potent potion of endurance","potent endurance","potion"}
title "a potent potion of endurance"
descr "A potent potion of endurance is here."
extra {}
"A clear glass vial contains a glowing green substance. It appears 
to be a potent potion of endurance."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("endurance","strong");

end

half_end
names {"potion of endurance","endurance","potion"}
title "a potion of endurance"
descr "A potion of endurance is here."
extra {}
"A clear glass vial contains a glowing green substance. It appears 
to be a potion of endurance."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("endurance","half");

end

full_mana
names {"potion of full mana","full mana","potion"}
title "a potion of full mana"
descr "A potion of full mana is here."
extra {}
"A clear glass vial contains a glowing blue substance. It appears 
to be a highly potent potion of mana."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("mana","full");

end

strong_mana
names {"potent potion of mana","potent mana","potion"}
title "a potent potion of mana"
descr "A potent potion of mana is here."
extra {}
"A clear glass vial contains a glowing blue substance. It appears 
to be a potent potion of mana."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("mana","strong");

end

half_mana
names {"potion of mana","mana","potion"}
title "a potion of mana"
descr "A potion of mana is here."
extra {}
"A clear glass vial contains a glowing blue substance. It appears 
to be a potion of mana."
manipulate {MANIPULATE_TAKE,MANIPULATE_HOLD}
weight 1
cost 1 IRON_PIECE
type ITEM_POTION

dilcopy restore_pot@bounty("mana","half");

end


mulligan
names {"contract breaker","note","mulligan"}
title "a contract breaking note"
descr "A contract breaking note is here."
extra {}
"This note, when given to the bounty hunter, allows you to break from a single contract."
manipulate {MANIPULATE_TAKE}
weight 1
cost 1 IRON_PIECE
end

mc_hint
names {"monthly continent hint","hint"}
title "a monthly continent hint"
descr "A monthly continent hint is here."
extra {}
"<div class='cpc'>HINT!!</div><br>"
manipulate {MANIPULATE_TAKE}
dilcopy hint@bounty("m","c");
weight 1
cost 1 IRON_PIECE
end

mz_hint
names {"monthly zone hint","hint"}
title "a monthly zone hint"
descr "A monthly zone hint is here."
extra {}
"<div class='cpc'>HINT!!</div><br>"
manipulate {MANIPULATE_TAKE}
dilcopy hint@bounty("m","z");
weight 1
cost 1 IRON_PIECE
end

dc_hint
names {"daily continent hint","hint"}
title "a daily continent hint"
descr "A daily continent hint is here."
extra {}
"<div class='cpc'>HINT!!</div><br>"
manipulate {MANIPULATE_TAKE}
dilcopy hint@bounty("d","c");
weight 1
cost 1 IRON_PIECE
end

dz_hint
names {"daily zone hint","hint"}
title "a daily zone hint"
descr "A daily zone hint is here."
extra {}
"<div class='cpc'>HINT!!</div><br>"
manipulate {MANIPULATE_TAKE}
dilcopy hint@bounty("d","z");
weight 1
cost 1 IRON_PIECE
end

package
names {"package", "packagefor"}
title "a lost package"
descr "A lost package lies here."
extra {}
"The package is nondescript, wrapped in brown paper and tied with string.  
You dare not open it."
weight 2
manipulate {MANIPULATE_TAKE}
dilcopy package@bounty();
end

%reset
load hunter into contracts max 1
load shopkeep into shop max 1
load bty_sign into contracts max 1
load pkg_sign into jewels max 1
load spree_sign into smithy max 1
load jeweler into jewels max 1
load blacksmith into smithy max 1
load historian into library max 1

%end
